### Алгоритм Кендалла на APL

##### Описание алгоритма

Даны матрица «объект-свойство» X, размера MxN и вектор классификации С, размера M.

C[I]=1, если I-ый объект X[I;] принадлежит классу 1 и

C[I]=2, если I-ый объект X[I;] принадлежит классу 2

##### ОБУЧЕНИЕ:

для всех признаков xi, i=1,2,...N

определить пороги «чистого отсечения» p1 и p2, такие, что

x≤p1 ==> x принадлежит одному классу

x≥p2 ==> x принадлежит другому классу

отсортировать признаки по информативности (числу точек в области перечечения)

запомнить результаты

##### КЛАССИФИКАЦИЯ:

если значение признака 1 попадает в “чистую” область выдать диагноз и прекратить выполнение алгоритма иначе перейти к следующему признаку и повторить классификацию

если все признаки перебраны выдать диагноз “неизвестный класс” и прекратить выполнение алгоритма.

### Функции

Число точек в области пересечения множеств (выборок)

Аргументы a ,b - выборки значений признака (векторы) для классов 1,2.

Результат n - число точек в области пересечения

```
      ∇ n←a cross b
[1]  n←+/b<⌈/a25
[2]  n←n⌊+/b>⌊/a25          ⍝сколько b>min a
[3]  ∇
```

Пример:

```
      1 2 3 cross 2.1 2.9 4 5
2      
```

### Правило для одного признака

Аргументы a,b - выборки значений признака (векторы) для классов 1,2.

Результат r — вектор 4-х элементов: порог ≤, класс для этого порога, порог ≥, класс для этого порога

```
      ∇ r←a rule b;c;ab      ⍝множество значений признака для b (b;c;ab)
[1]  c←∊(⍴¨a b)⍴¨1 2
[2]  c←c[⍋a,b]
[3]  ab←(a,b)[⍋a,b]
[4]  r←(⌈/(∧\c=↑c)/ab),↑c
[5]  r←r,(⌊/(⌽∧\( ¯1↑c)=⌽c)/ab), ¯1↑c
[6]  ∇
```

Пример:

```
      1 2 3 rule 2.1 2.9 4 5
2 1 4 2
```

что означает:

если x≤2 то класс 1

если x≥4 то класс 2

### Набор всех правил

Аргументы a,b — векторы векторов значений всех признаков для класса 1 (a) и для класса 2 (b)

Результат r — вектор векторов всех правил с номерами признаков, признаки отсортированы по информативности (числу точек в области пересечения).

```
      ∇ r←a rules b;i
[1]  i←⍋a cross¨b
[2]  r←a rule¨b
[3]  r←(⍳⍴a),¨r
[4]  r←r[i]
[5]  ∇
```

Пример:

```
      (1 2 3)(9 4 8) rules (2.1 2.9 4 5)(3 4.5 1 2)
2 3 2 8 1 1 2 1 4 2
      disp (1 2 3)(9 4 8) rules (2.1 2.9 4 5)(3 4.5 1 2)
.→------------------------.
| .→--------. .→--------. |
| |2 3 2 8 1| |1 2 1 4 2| |
| '~--------' '~--------' |
'∊------------------------'
```

слева: x<3 - класс 2; x>8 - класс 1          справа: x<2 - класс 1; x>4 - класс 2

### Классификация по одному правилу

Аргумент (правый) x — точка подлежащая классификации

Аргумент (левый) r — правило в формате функции rule

Результат — номер класса

```
       ∇ c←r class x
[1]  c←0
[2]  ⍎(x≤r[1])/'c←r[2]'
[3]  ⍎(x≥r[3])/'c←r[4]'
[4]  ∇
```

Пример:

```
      2 1 4 2 class 0
1
      2 1 4 2 class 5
2
      2 1 4 2 class 3 ⍝ do not know
0
```

### Диагностирование (классификация по всем правилам)

Аргумент (правый) x — точка подлежащая классификации

Аргумент (левый) rules — все правила в формате функции rules

Результат — номер класса

```
      ∇ d←rules diag x;i;r
[1]  i←1
[2]  L:r←i⊃rules
[3]  d←(1↓r)class x[↑r]
[4]  →(d≠0)/0
[5]  →((⍴rules)≤i←i+1)/L
[6]  ∇
```

Применение для выборки ирисов

```
ir1: 50x4 matrix of class 1 of Iris
ir2: 50x4 matrix of class 2 of Iris
ir3: 50x4 matrix of class 3 of Iris
```

Применение правил для классов 1 и 2

```
      rr←(⊂[1]ir1) rules ⊂[1]ir2
      disp rr
.→------------------------------------------------------------.
| .→----------. .→----------. .→------------. .→------------. |
| |3 1.9 1 3 2| |4 0.6 1 1 2| |1 4.9 1 5.8 2| |2 2.2 2 3.5 1| |
| '~----------' '~----------' '~------------' '~------------' |
'∊------------------------------------------------------------'
```

Проверка точности классификации

```
      +d←(⊂rr) diag ̈ ⊂[2]ir1
1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
      d∧.=1
1
      100×(+/d=1)÷⍴d
100
      d←(⊂rr) diag ̈ ⊂[2]ir2
      100×(+/d=2)÷⍴d
100      
```

##### !!!признаки - спектральные амплитуды на заданных частотах